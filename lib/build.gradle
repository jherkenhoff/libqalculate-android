plugins {
    id 'com.android.library'
    id 'maven-publish'
    id 'signing'
}

group = 'com.jherkenhoff'

android {
    namespace = 'com.jherkenhoff.libqalculate'
    compileSdk = 36

    defaultConfig {
        minSdk 24
        versionCode 3
        versionName "5.8.2-1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ''
                arguments "-DANDROID_ARM_NEON=TRUE","-DANDROID_STL=c++_shared"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
            withJavadocJar()
        }
    }
    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
            version '3.22.1'
        }
    }
    buildFeatures {
        viewBinding true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release


                groupId = 'com.jherkenhoff'

                artifactId rootProject.name
                version = rootProject.version

                pom {
                    name = rootProject.name
                    description = 'Port and precompiled binaries of the qalculate c library for android'
                    url = 'https://github.com/jherkenhoff/libqalculate-android'
                    packaging = 'aar'
                    inceptionYear = '2023'
                    licenses {
                        license {
                            name = 'GPL-2.0'
                            url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            id = "jherkenhoff"
                            name = "Jost Herkenhoff"
                            email = "jost.herkenhoff@mailbox.org"
                        }
                    }
                    scm {
                        url = 'https://github.com/jherkenhoff/libqalculate-android'
                        connection = 'https://github.com/jherkenhoff/libqalculate-android.git'
                    }
                }
            }
        }
        repositories {
            maven {
                url = layout.buildDirectory.dir('staging-deploy')
            }
        }
    }
}

project.afterEvaluate {
    // From: https://github.com/sureshjoshi/android-ndk-swig-example/blob/master/AS3/app/build.gradle

    // Not sure how much of a hack this is - but it allows CMake/SWIG to run before Android Studio
    // complains about missing generated files
    // TODO: Probably need a release hook too?
    javaPreCompileDebug.dependsOn externalNativeBuildDebug
}