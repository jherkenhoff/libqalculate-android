plugins {
    id 'com.android.library'
    id 'maven-publish'
    id "com.vanniktech.maven.publish" version "0.35.0"
}

group = 'com.jherkenhoff'

android {
    namespace = 'com.jherkenhoff.libqalculate'
    compileSdk = 36

    defaultConfig {
        minSdk 24
        versionCode 3
        versionName "5.8.2-1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        externalNativeBuild {
            cmake {
                cppFlags ''
                arguments "-DANDROID_ARM_NEON=TRUE","-DANDROID_STL=c++_shared"
            }
        }
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    externalNativeBuild {
        cmake {
            path file('CMakeLists.txt')
            version = '3.22.1'
        }
    }
    buildFeatures {
        viewBinding = true
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
}

dependencies {
    androidTestImplementation 'androidx.test:runner:1.6.2'
    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
}

mavenPublishing {
    publishToMavenCentral()
    signAllPublications()

    coordinates("com.jherkenhoff", "libqalculate", "5.8.2-1")

    pom {
        name = rootProject.name
        description = 'Port and precompiled binaries of the qalculate c library for android'
        url = 'https://github.com/jherkenhoff/libqalculate-android'
        inceptionYear = '2023'
        licenses {
            license {
                name = 'GPL-2.0'
                url = 'https://www.gnu.org/licenses/old-licenses/gpl-2.0.txt'
                distribution = 'repo'
            }
        }
        developers {
            developer {
                id = "jherkenhoff"
                name = "Jost Herkenhoff"
            }
        }
        scm {
            url = 'https://github.com/jherkenhoff/libqalculate-android'
            connection = "scm:git:git://github.com/jherkenhoff/libqalculate-android.git"
            developerConnection = "scm:git:ssh://git@github.com/jherkenhoff/libqalculate-android.git"
        }
    }
}